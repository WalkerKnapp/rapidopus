import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'maven'
}

group 'com.walker'
version '1.0.0'

archivesBaseName = 'rapidopus'

sourceCompatibility = 9

task generateJniHeaders(type: Exec, description: 'Generates C headers for JNI by running javah', group: 'build') {
    def nativeIncludes = "../rapidopus-natives/src/main/headers"

    ArrayList<String> javacCommand = new ArrayList<String>()
    javacCommand.add(Jvm.current().javacExecutable.toString())
    javacCommand.add("-d")
    javacCommand.add("build/tmp/generateJniHeaders")
    javacCommand.add("-h")
    javacCommand.add(nativeIncludes)

    sourceSets.main.allJava.forEach({ sourceFile ->
        javacCommand.add(sourceFile.toPath().toAbsolutePath().toString())
    })

    commandLine javacCommand.toArray()
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
