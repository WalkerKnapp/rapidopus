import org.gradle.internal.jvm.Jvm

plugins {
    base
    `java-library`
    `maven-publish`
    signing
}

base.archivesBaseName = "rapidopus"
java.sourceCompatibility = JavaVersion.VERSION_1_9

val sourceJar by tasks.creating(Jar::class) {
    from(sourceSets.main.get().allJava)
    this.archiveClassifier.set("sources")
}

val javadocJar by tasks.creating(Jar::class) {
    dependsOn("javadoc")
    from(tasks.javadoc.get().destinationDir)
    this.archiveClassifier.set("javadoc")
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        create<MavenPublication>("rapidopus") {
            from(components["java"])
            artifact(sourceJar)
            artifact(javadocJar)

            groupId = project.group as String
            artifactId = "rapidopus"
            version = project.version as String?

            pom {
                name.set("RapidOpus")
                description.set("A performance-oriented Java wrapper for libopus using JNI.")
                url.set("https://github.com/WalkerKnapp/rapidopus")

                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }

                developers {
                    developer {
                        id.set("WalkerKnapp")
                        name.set("Walker Knapp")
                        email.set("walker@walkerknapp.me")
                    }
                }

                scm {
                    connection.set("scm:git:git://github.com/WalkerKnapp/rapidopus.git")
                    developerConnection.set("scm:git:git@github.com:WalkerKnapp/rapidopus.git")
                    url.set("https://github.com/WalkerKnapp/rapidopus")
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(System.getenv("PGP_KEY_ID"), System.getenv("PGP_KEY"), System.getenv("PGP_PASSWORD"))
    sign(publishing.publications["rapidopus"])
}

// Generate an artifact of the JNI headers created by this project, for rapidopus-natives to consume.


val generateJniHeaders by tasks.registering(Exec::class) {
    description = "Generates C headers for JNI by running javah"
    group = "build"

    inputs.files(sourceSets.main.get().allJava)
    outputs.dir(temporaryDir.resolve("headers").absolutePath)

    doFirst {
        temporaryDir.resolve("classes").mkdirs()
        temporaryDir.resolve("headers").mkdirs()

        val javacCommand = mutableListOf(Jvm.current().javacExecutable.toString(),
                "-d",
                temporaryDir.resolve("classes").absolutePath,
                "-h",
                temporaryDir.resolve("headers").absolutePath)

        inputs.files.forEach { file ->
            javacCommand.add(file.toPath().toAbsolutePath().toString())
        }

        commandLine(javacCommand)
    }
}

val jniIncludes: Configuration by configurations.creating {
    isCanBeConsumed = true
    isCanBeResolved = false
    attributes {
        attribute(Usage.USAGE_ATTRIBUTE, namedAttribute(Usage.C_PLUS_PLUS_API))
        attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, namedAttribute(LibraryElements.HEADERS_CPLUSPLUS))
        attribute(Attribute.of("artifactType", String::class.java), "directory")
    }
}
inline fun <reified T: Named> Project.namedAttribute(value: String) = objects.named(T::class.java, value)

artifacts {
    add("jniIncludes", generateJniHeaders) {
        name = "jni-headers"
        type = "directory"
        builtBy(generateJniHeaders)
    }
}

// Depend on the artifacts generated by rapidopus-natives


val nativeDependency: Configuration by configurations.creating {
    extendsFrom(configurations["implementation"], configurations["runtimeOnly"])
}

dependencies {
    nativeDependency(project(":rapidopus-natives", "nativeArtifacts"))
}

tasks.jar {
    dependsOn(nativeDependency)
    from(nativeDependency.map { zipTree(it) })
}
